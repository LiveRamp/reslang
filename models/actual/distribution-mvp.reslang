
request-resource DistributionRequest {
    id: string

      "Reference to a destination's endpoints to be used for distributing the data"
    destinationEndpointId: linked Destination::Endpoint

      "A map of keys and values needed to use the selected destination endpoint"
    destinationEndpointProperties: stringmap<string>

      "URL of the data to be distributed. Currently, we only support files in GCS"
    inputDataURL: url

    "Mapping configs required for a given platform. See docs: DistMVP.dataMappingConfig"
    datMappingConfigs: DataMappingConfig[]

      "Datetime in UTC, formatted as ISO-8601, when the request was created"
    createdAt: datetime output

      "Status of the given DistributionRequest"
    status: StatusEnum output query

      "Datetime in UTC, formatted as ISO-8601, when the status of the request last changed"
    statusUpdatedAt: datetime output

      "Metadata about the batch output"
    outputMetaData: BatchOutputMetadata output
    
    distributionType: DistributionTypeEnum output

    // query only params

      "Sort key and order. See docs: DistMVP.sort"
    sort: SortTypeEnum queryOnly

      "Filter the list of DistributionRequests by when it was created. See docs: DistMVP.createdAfter"
    createdAfter: datetime queryOnly

      "Filter the list of DistributionRequests by when it was created. See docs: DistMVP.createdBefore"
    createdBefore: datetime queryOnly

      "Filter the list of DistributionRequests by when its status was last updated. See docs: DistMVP.statusUpdatedAfter"
    statusUpdatedAfter: datetime queryOnly

      "Filter the list of DistributionRequests by when its status was last updated. See docs: DistMVP.statusUpdatedBefore"
    statusUpdatedBefore: datetime queryOnly

    /operations
          "Get a request. See docs: DistMVP.GET"
        GET
          "Use this endpoint to create a new DistributionRequest"
        POST
          "Use this endpoint to find existing DistributionRequests. See docs: DistMVP.MULTIGET"
        MULTIGET
}

"Use this action to cancel a given DistributionRequest. See docs: DistMVP.Cancel"
action DistributionRequest::Cancel {
    id: int

    /operations
    POST
}

"Use this action to retry a failed DistributionRequest. See docs: DistMVP.Retry"
action DistributionRequest::Retry {
    id: int

    /operations
    POST
}

enum DistributionTypeEnum {
    BATCH
}

enum SortTypeEnum {
    CREATED_AT_DESC
    CREATED_AT_ASC
    STATUS_UPDATED_DESC
    STATUS_UPDATED_ASC
}

enum StatusEnum {
    QUEUED
    IN_PROGRESS
    COMPLETED
    FAILED
    CANCELLED
}

structure DataMappingConfig {
    input: MappingInputUnion;
    output: MappingOutputUnion
}

union MappingInputUnion {
    keyValue: MappingInputKey inline
}

structure MappingInputKey {
    key: int
    value: int
}

union MappingOutputUnion {
    id: MappingIdLabel inline
    keyValue: MappingKeyValueLabel inline
}

structure MappingKeyValueLabel {
    keyLabel: string
    valueLabel: string optional
}

structure MappingIdLabel {
    idLabel: string
}

structure BatchOutputMetadata {
    fileDetails: FileLineCounts[]
    "Total size of the distributed data in bytes"
    totalOutputSize: int
    
    "The number of distribution records in the file initially. See docs: DistMVP.TotalRecords"
    totalDistributionRecords: int
}

structure FileLineCounts {
    fileName: string
    lineCount: int
}

/**
  future resources, for linking
  */

future configuration-resource Destination {
    id: int
}

future subresource Destination::Endpoint {
    id: int
} 